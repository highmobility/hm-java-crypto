buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.13.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: "java-library"

repositories {
    jcenter()
    maven {
        url "http://jfrog.high-mobility.com/artifactory/gradle-dev-local/"
        credentials {
            username devReadUser
            password devReadPassword
        }
    }
}

def artifact = new Properties()
artifact.load(new FileInputStream("$projectDir/deploy.settings"))

group = artifact.groupId
version = artifact.version

sourceCompatibility = 1.8

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        corejni   : "1.0.1",
        utils     : "1.4.4",
        autoApi   : "3.11.1",
        compileSdk: 28
]

ext.deps = [
        "corejni": "com.highmobility:hmkit-core-jni:${ver.corejni}",
        "utils"  : "com.highmobility:hmkit-utils:${ver.utils}",
        "autoApi": "com.highmobility:hmkit-auto-api:${ver.autoApi}",
        "timber" : "com.jakewharton.timber:timber:4.7.1",
]

dependencies {
    if (project.hasProperty('releaseType') == false || project.releaseType > 0) {
        // standalone or release project
        api deps.utils
        api deps.corejni
        testImplementation deps.autoApi
    } else {
        // in the master project with dev config
        implementation project(':hmkit-core-jni')
        implementation project(':hmkit-utils')
        testImplementation project(':hmkit-auto-api')
    }

    testImplementation 'junit:junit:4.12'
}

// deploy script
apply from: "https://bitbucket.org/tonisives/tools/raw/master/scripts/deploy-java.gradle"