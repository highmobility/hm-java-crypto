buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1+'
        classpath 'com.github.dcendents:android-maven-plugin:1+'
    }
}

apply plugin: 'java'

def artifact = new Properties()
artifact.load(new FileInputStream("$projectDir/deploy.settings"))

group = artifact.groupId
version = artifact.version

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url "http://jfrog.high-mobility.com/artifactory/gradle-dev-local/"
        credentials {
            username System.properties['devReadUser']
            password System.properties['devReadPassword']
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        crypto    : "1.4.3",
        utils     : "1.4.3",
        hmkit     : "1.6.8",
        autoApi   : "2.10.0",
        compileSdk: 28
]

ext.deps = [
        "hmkit"   : "com.highmobility:hmkit-android:${ver.hmkit}@aar",
        "crypto"  : "com.highmobility:hmkit-crypto:${ver.crypto}",
        "utils"   : "com.highmobility:hmkit-utils:${ver.utils}",
        "autoApi" : "com.highmobility:hmkit-auto-api:${ver.autoApi}",
        "timber"  : "com.jakewharton.timber:timber:4.7.1",
        "volley"  : "com.android.volley:volley:1.1.1",
        "findBugs": 'com.google.code.findbugs:jsr305:3.0.2'
]

dependencies {
    if (project.hasProperty('releaseType') == false || project.releaseType > 0) {
        // standalone or release project
        implementation deps.utils
    } else {
        // in the master project with dev config
        implementation project(':hmkit-utils')
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation deps.autoApi
}

// deploy

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

version = artifact.version
group = artifact.groupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId artifact.groupId
            artifactId artifact.id
            version artifact.version
            name artifact.id // pom.project.name must be same as bintray.pkg.name
            url artifact.siteUrl
            inceptionYear '2018'
            licenses {
                license { // HARDCODED
                    name 'GPL-3.0'
                    url 'https://www.gnu.org/licenses/gpl.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection artifact.gitUrl
                developerConnection artifact.gitUrl
                url artifact.siteUrl
            }
        }
    }
}

bintray {
    user = System.properties['hmBintrayUser']
    key = System.properties['hmBintrayApikey']

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = artifact.id
        websiteUrl = artifact.siteUrl
        vcsUrl = artifact.gitUrl
        licenses = artifact.licenses
        publish = true
        version {
            name = artifact.version
        }
    }
}

jar {
    exclude('**/btcore')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// artifactory

artifactory {
    contextUrl = System.properties['hmArtifactoryUrl']

    publish {
        repository {
            repoKey = project.hasProperty("repo") ? project.property("repo") : artifact.repo
            username = System.properties['hmArtifactoryPublishUser']
            password = System.properties['hmArtifactoryPublishPassword']
            maven = true
        }

        defaults {
            publishConfigs('archives', 'published')
            publishPom = true
            publishIvy = false
        }
    }
}