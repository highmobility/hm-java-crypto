buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.13.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "com.jfrog.artifactory"
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
    maven {
        url "http://jfrog.high-mobility.com/artifactory/gradle-dev-local/"
        credentials {
            username devReadUser
            password devReadPassword
        }
    }
}

def artifact = new Properties()
artifact.load(new FileInputStream("$projectDir/deploy.settings"))

group = artifact.groupId
version = artifact.version

sourceCompatibility = 1.8

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        corejni   : "1.0.0",
        utils     : "1.4.4",
        autoApi   : "3.11.1",
        compileSdk: 28
]

ext.deps = [
        "corejni": "com.highmobility:hmkit-core-jni:${ver.corejni}",
        "utils"  : "com.highmobility:hmkit-utils:${ver.utils}",
        "autoApi": "com.highmobility:hmkit-auto-api:${ver.autoApi}",
        "timber" : "com.jakewharton.timber:timber:4.7.1",
]

dependencies {
    if (project.hasProperty('releaseType') == false || project.releaseType > 0) {
        // standalone or release project
        api deps.utils
        api deps.corejni
        testImplementation deps.autoApi
    } else {
        // in the master project with dev config
        implementation project(':hmkit-core-jni')
        implementation project(':hmkit-utils')
        testImplementation project(':hmkit-auto-api')
    }

    testImplementation 'junit:junit:4.12'
}

// deploy
//

// add the sources and doc artifacts

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// artifactory

artifactory {
    contextUrl = hmArtifactoryUrl

    publish {
        repository {
            repoKey = project.hasProperty("repo") ? project.property("repo") : artifact.repo
            username = hmArtifactoryPublishUser
            password = hmArtifactoryPublishPassword
        }

        defaults {
            publishConfigs('archives', 'published')
            publishIvy = false
        }
    }
}

// bintray

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId artifact.groupId
            artifactId artifact.id
            version artifact.version
            name artifact.id // pom.project.name must be same as bintray.pkg.name
            url artifact.siteUrl
            inceptionYear '2018'
            licenses {
                license { // HARDCODED
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                connection artifact.gitUrl
                developerConnection artifact.gitUrl
                url artifact.siteUrl
            }
        }
    }
}

bintray {
    user = hmBintrayUser
    key = hmBintrayApikey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = artifact.id
        websiteUrl = artifact.siteUrl
        vcsUrl = artifact.gitUrl
        licenses = artifact.licenses

        version {
            name = artifact.version
        }
    }
}