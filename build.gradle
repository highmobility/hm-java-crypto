buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

apply plugin: "java-library"

repositories {
    jcenter()
    if (project.hasProperty('devReadUser')) {
        maven {
            url "http://jfrog.high-mobility.com/artifactory/gradle-dev-local/"
            credentials {
                username devReadUser
                password devReadPassword
            }
        }
    }
}

sourceCompatibility = 1.8

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        "hmkit-core-jni"    : "1.0.2-dev",
        "hmkit-crypto"      : "1.4.6-dev",
        "hmkit-utils"       : "1.4.5-dev",
        "hmkit-auto-api"    : "3.12.0-dev",
        compileSdk          : 28
]

ext.deps = [
        "utils"   : "com.highmobility:hmkit-utils:${ver."hmkit-utils"}",
        "corejni" : "com.highmobility:hmkit-core-jni:${ver."hmkit-core-jni"}",
        "autoApi" : "com.highmobility:hmkit-auto-api:${ver."hmkit-auto-api"}",
        "timber"  : "com.jakewharton.timber:timber:4.7.1",
]

dependencies {
    if (project.hasProperty('depLocation') == false || project.depLocation > 0) {
        // standalone or release project
        api deps.utils
        api deps.corejni
        testImplementation deps.autoApi
    } else {
        // in the master project with dev config
        implementation project(':hmkit-core-jni')
        implementation project(':hmkit-utils')
        testImplementation project(':hmkit-auto-api')
    }

    testImplementation 'junit:junit:4.12'
}

// deploy script
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/deploy-java-v2.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/test-shell-logging.gradle"